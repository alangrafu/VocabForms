{{lodspk.prefixes}}

SELECT DISTINCT ?predicate (SAMPLE (?pl) AS ?predLabel) ?predRange ?pos WHERE {
  GRAPH <{{lodspk.instances}}>{
      <{{lodspk.args.all|deurifier}}> a ?class .
  }
  GRAPH  <{{lodspk.vocab}}>{  
    ?class rdfs:subClassOf * ?y.
    ?predicate rdfs:domain ?y ;
               a owl:ObjectProperty. 
    OPTIONAL { ?predicate rdfs:label ?pl }
    OPTIONAL { ?predicate rdfs:range ?predRange }
    FILTER NOT EXISTS { ?subprop rdfs:subPropertyOf ?predicate }

  }
  GRAPH <{{lodspk.layout}}>{
    {
      ?predicate layout:hasLayoutParameterSet ?lps. 
      ?lps layout:shown "true"^^xsd:boolean .
      OPTIONAL{ ?lps layout:position ?pos }
    }UNION{
      FILTER NOT EXISTS{
      ?predicate layout:hasLayoutParameterSet ?x 
      }
    }
  }
    FILTER((bound(?pos) && ?pos >= 0)||(!bound(?pos)))
}GROUP BY ?predicate ?predRange ?pos
ORDER BY DESC(?pos) ?predLabel ?predicate

