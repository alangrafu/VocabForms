{%for h in base.header %}
PREFIX {{h.prefix}}: <{{h.ns}}>
{%endfor%}
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX vocab: <http://poderopedia.com/vocab/>
PREFIX layout: <http://graves.cl/layout/>

SELECT DISTINCT ?predicate (SAMPLE (?pl) AS ?predLabel) ?predRange ?pos WHERE {
  GRAPH <{{lodspk.instances}}>{
      <{{lodspk.args.all|deurifier}}> a ?class .
  }
  GRAPH ?g{  
    ?class rdfs:subClassOf * ?y.
    ?predicate rdfs:domain ?y ;
               a owl:ObjectProperty. 
    OPTIONAL { ?predicate rdfs:label ?pl }
    OPTIONAL { ?predicate rdfs:range ?predRange }
    FILTER NOT EXISTS { ?subprop rdfs:subPropertyOf ?predicate }

  }
  OPTIONAL{
    GRAPH <{{lodspk.layout}}>{
      ?predicate layout:position ?pos.    
    }
  }
  FILTER((bound(?pos) && ?pos >= 0)||(!bound(?pos)))
}GROUP BY ?predicate ?predRange ?pos
ORDER BY DESC(?pos) ?predLabel ?predicate

